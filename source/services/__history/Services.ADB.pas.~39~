unit Services.ADB;

interface

uses
  Services.IDE, System.Classes, System.SysUtils;

type
  TADBService = class(TInterfacedObject, IADBServices)
  private
    function GetAdbPath(): string;
    procedure ListDeviceProps(const ADevice: string; const AStrings: TStrings);
    function GetDeviceVendorModel(const ADeviceProps: TStrings): string;

    procedure EnumDevices(const ADeviceList: TStrings; const AProc: TProc<string>);
    function FindDeviceVendorModel(const ADevice: string): string;
  public
    procedure ListDevices(const AStrings: TStrings);
  end;

implementation

uses
  Storage.Default,
  Model.Environment,
  {$IFDEF MSWINDOWS}
  Services.ADB.Win;
  {$ELSE}
  Services.ADB.Posix;
  {$ENDIF}

{ TADBService }

procedure TADBService.EnumDevices(const ADeviceList: TStrings;
  const AProc: TProc<string>);
begin
  if not Assigned(AProc) then
    Exit;

  if ADeviceList.Count > 1 then begin
    for var I := 1 to ADeviceList.Count -1 do begin
      if ADeviceList[I].Trim().IsEmpty() then
        Exit;

      var LPos := Pos(#9, ADeviceList[I]);
      if LPos = -1 then
        Exit;

      var LDevice := Copy(ADeviceList[I], 1, LPos - 1);
      var LValue: extended;
      if TryStrToFloat(LDevice, LValue) then
        AProc(LDevice);
    end;
  end;
end;

function TADBService.FindDeviceVendorModel(const ADevice: string): string;
begin
  var LStrings := TStringList.Create();
  try
    ExecCmdine(GetAdbPath() + Format(' -s %s shell getprop ro.product.model', [ADevice]), LStrings);
    Result := LStrings.Text;
  finally
    LStrings.Free();
  end;
end;

function TADBService.GetAdbPath: string;
begin
  var LStorage := TDefaultStorage<TEnvironmentModel>.Make();
  var LModel: TEnvironmentModel := nil;
  if LStorage.LoadModel(LModel) then
    Result := LModel.AdbLocation
  else
    Result := string.Empty;
end;

function TADBService.GetDeviceVendorModel(const ADeviceProps: TStrings): string;
const
  VENDOR_MODEL_STR = '[ro.product.vendor.model]:';
begin
  for var LLine in ADeviceProps do
  begin
    if (LLine.StartsWith(VENDOR_MODEL_STR)) then begin
      var LVendorModel := LLine
        .Replace(VENDOR_MODEL_STR, String.Empty)
        .Replace('[', String.Empty)
        .Replace(']', String.Empty)
        .Trim();
      Exit(LVendorModel);
    end;
  end;
  Result := String.Empty;
end;

procedure TADBService.ListDeviceProps(const ADevice: string;
  const AStrings: TStrings);
begin
  ExecCmdine(GetAdbPath() + Format(' -s %s shell getprop', [ADevice]), AStrings);
end;

procedure TADBService.ListDevices(const AStrings: TStrings);
begin
  var LStrings := TStringList.Create();
  try
    ExecCmdine(GetAdbPath() + ' devices', LStrings);
    EnumDevices(LStrings, procedure(ADevice: string) begin
//      var LDeviceProps := TStringList.Create();
//      try
//        ListDeviceProps(ADevice, LDeviceProps);
//        AStrings.Add( GetDeviceVendorModel(LDeviceProps) );
//      finally
//        LDeviceProps.Free();
//      end;
      AStrings.Add(FindDeviceVendorModel(ADevice));
    end);
  finally
    LStrings.Free();
  end;
end;

end.
